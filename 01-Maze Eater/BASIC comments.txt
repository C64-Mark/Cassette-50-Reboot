Puts zero into 12288 to 13504 - sprite/char memory
2 fora=192*64to210*64+64:pokea,0:next

Clear SID registers
3 fora=54272to54296:pokea,0:next

bb$=array for “enter level”  ga=game (0=set up, 1=already set up)
4 dimbb$(11):ga=0

b$= highscore names
5 b$(1)="{red}0000000 rich":b$(2)="{cyan}0000000 mick":b$(3)="{blue}0000000 hibb"
6 b$(4)="{green}0000000 dave":b$(5)="x0000000 grey"

Score text
9 s$="0000000"

Border & background= black. Disable all sprites. le=level  hh=ghost frame   do=eaten count
10 poke53280,0:poke53281,0:v=53248:pokev+21,0:le=1:hh=205:do=0

Title screen
20 print"{clear}{down*7}"
30 print"{yellow}wwwww wwww wwww wwww"
31 print"{red}w{sh space}w{sh space}w{sh space}w{sh space} w{sh space*4}w{sh space}w{space*3}"
32 print"{purple}w{sh space}w{sh space}w{sh space}w{sh space*2}w{sh space*3}w{space*2}w{space*3}"
33 print"{blue}w{sh space}w{sh space}w{sh space}wwww{sh space*2}w{sh space*3}www{space*2}{yellow}e a t e r"
40 print"{green}w{sh space}w{sh space}w{sh space}w{sh space*2}w{sh space}w{sh space*4}w{space*3}"
50 print"{dark gray}w{sh space}w{sh space}w{sh space}w{space*2}w wwww wwww"
60 print"{cm y*21}"
61 print"{yellow}press f1 to change level"
70 print"use joystick in port 1{space*2}or keys 1=up{space*4}{arrow left}=down{space*2}(ctrl)=left and 2=right
80 print"{white}{down}{reverse on}{space*8}please wait reading data{space*8}"

Expand fruit sprite in x & y direction. hi-score=5000, c=pac screen loc (1470=row 11)  c1=ghost screen loc (1228=row 5)  fr=fruit sprite score=0
90 pokev+29,4:pokev+23,4:hi=5000:c=1479:c1=1228:fr=191:a$="{blue}":sc=0

Pac spr=200, ghost spr=205, fruit spr=102. Keyboard repeat on. se=never used  co=level change flag  lives=3
110 poke2040,200:poke2041,205:poke2042,192:poke650,255:se=0:co=0:li=3

at=att/dec, vo=vol, wa=wave, h=freq, su=sus/re. Disable sprites. Ghost sprcol = red
150 at=54277:vo=54296:wa=54276:h=54273:su=54278:pokev+21,0:pokev+40,2


Sound effects
160 pokeat,32:pokeat+7,32:pokesu,255:pokesu+7,255:pokewa,33:pokewa+7,33

Expand fruit sprite in x&y direction. Volume=max
190 pokev+23,4:pokev+29,4:pokevo,15

If game already set-up, skip to 226
195 ifga=1then226

Stop timer. Switch char rom visible at D000
200 poke56334,peek(56334)and254:poke1,peek(1)and251

Copy char rom from D000 to 3800
210 fori=0to511:pokei+14336,peek(i+53248):next

Char rom not visible at d000. Start timer
220 poke1,peek(1)or4:poke56334,peek(56334)or1

If game not set up then set up chars and sprites
226 ifga=0thengosub1230

Print title screen. Move char rom to $3800
230 gosub10000:poke53272,31

Set x&y for pacman and ghost. Ghost spr=multicol, multic=white. Fruit Spr xy= 125,120
231 x=138:y=133:x1=50:y1=80:pokev+28,2:pokev+37,1:x6=125:y6=120

Set fruit sprite x and y. Border and background=orange
232 pokev+4,x6:pokev+5,y6:pokev+32,8:pokev+33,8
260 goto810

Fruit Sprite colour is red
270 pokev+42,2

Print hi score, lives, a$=fruit char on screen right
271 print"{home}{down*8}{blue}";tab(30);hi
272 print"{home}{down*12}";tab(30);li
273 print"{home}{down*20}";tab(31);a$

Poll keyboard/joystick. Clear sprite/sprite col detector. Set fruit frame. jv=polled direction
280 jv=peek(56321):jv=15-(jvand15):pokev+30,0:poke2042,fr

If dots eaten=100 and fruit place=0 (i.e. not active) then enable fruit sprite
300 ifdo=100andfp=0thenpokev+21,7

Print score on right
310 print"{home}{down*4}{blue}";tab(30);sc


If first level then jump to normal move routine (i.e. with walls)
311 ifco=0then400

This section ignore walls on the second level
Check for left
360 ifco=1andjv=4thenx=x-8:c=c-1:poke2040,202

Check for right
370 ifco=1andjv=8thenx=x+8:c=c+1:poke2040,200

Check for up
380 ifco=1andjv=1theny=y-8:c=c-40:poke2040,202

Check for down
390 ifco=1andjv=2theny=y+8:c=c+40:poke2040,200

Skip normal move routine check
395 ifco=1then440

Check movement. c=char next to pacman. Move and update if not wall
400 ifpeek(c-1)<>42andco=0andjv=4thenx=x-8:c=c-1:poke2040,202:goto440
410 ifpeek(c+1)<>42andco=0andjv=8thenx=x+8:c=c+1:poke2040,200:goto440
420 ifpeek(c-40)<>42andco=0andjv=1theny=y-8:c=c-40:poke2040,202:goto440
430 ifpeek(c+40)<>42andco=0andjv=2theny=y+8:c=c+40:poke2040,200

Move ghost in direction of pac (higher level = quick movement). Update ghost sprite
440 ifx<x1thenx1=x1-le:poke2041,208
450 ifx>x1thenx1=x1+le:poke2041,206
460 ify>y1theny1=y1+le:poke2041,207
470 ify<y1theny1=y1-le:poke2041,205

Update sprite positions. Error here as x3/y3 should be x6/y6 - but fruit doesn’t move so ok
480 pokev,x:pokev+1,y:pokev+2,x1:pokev+3,y1:pokev+6,x3:pokev+7,y3

s=2 is the third level. So if you go off the screen you die
485 ifs<>2then506

If x goes out of bounds of the screen then go to lose life routine.
490 ifx<25thengosub710
495 ifx>245thengosub710
500 ify>225thengosub710
505 ify<50thengosub710

do=totals dots and pills. Level not finished until 272 collected
506 ifdo<272then560

If second level is complete gosub routine turns everything purple. Goto game display routine
510 ifdo=272ands=2thenpokev+32,0:pokev+33,0:ee=0:en=0:gosub770:goto810

Disable fruit sprite. rr=not used   ee=eaten   en=eaten count
520 ifdo=272thens=s+1:pokev+21,3:rr=rr+1:ee=0:en=0

Second level with black background. Border also black. Reseat do and ee. Gosub screen display and increase level speed
530 ifdo=272ands=2thenco=1:pokev+33,0:pokev+32,0:do=0:ee=0:gosub810:le=le+1

Sets the background black and resets the screen if about to start second level
540 ifdo=272ands=1thenpokev+33,0:pokev+32,0:do=0:ee=0:en=0:gosub810

If current char is a space then goto 580
545 ifpeek(c)=32then580

If char=81 which is a pill then ee=1 (eaten) and en=30 (eaten countdown). Play sound
560 ifpeek(c)=81thenee=1:en=30:fora=0to100step30:pokeh,a*2:forb=0to50:nextb,a

Char is one of 46 (dot), 81(pill) or 99(not implemented?) then replace with space and increase score by ten. Increase do (count of items) and gosub sound effect
570 ifpeek(c)=46orpeek(c)=81orpeek(c)=99thenpokec,32:sc=sc+10:do=do+1:gosub790

Check sprite to sprite collision for pac and gosub collision routine
580 if(peek(v+30)and1)=1thengosub1130

No pill eaten jump to 635
581 ifee=0then635

Countdown has ended, make ghost red
585 ifen=0thenpokev+40,2

If eaten then decrease the eaten counter
590 ifee=1thenen=en-1

If the eaten counter falls below zero then reset counter and eaten flag
600 ifen<0thenen=0:ee=0

If pill is eaten the change colour of ghost (based on eaten timer)
610 ifee=1thenpokev+40,en+1

Make a sound if ghost flashing
620 ifen>0thenpokeh+7,en

Stop sound once ghost stops flashing
630 ifen<1thenpokeh+7,0

Update pac sprite frame. Main gam loop
635 poke2040,201:goto280


Changes the colour of the fruit sprite, depending on fruit. A$ shows the fruit in place of these chars
650 iffr=192thenpokev+41,2:a$=a$+"{blue}A"
660 iffr=193thenpokev+41,2:a$=a$+"{blue}S"
670 iffr=194thenpokev+41,9:a$=a$+"{blue}X"
680 iffr=195thenpokev+41,5:a$=a$+"{blue}Z"
690 iffr=196thenpokev+41,7:a$=a$+"{blue}W"
700 return

Reduce lives. Go to 20000 if dead
710 li=li-1:ifli=0then20000

Update lives on screen
711 print"{home}{down*12}{blue}";tab(30);li

Reset sprite positions. Go to life lost sound routine
720 x=138:y=133:x1=50:y1=80:c=1479:c1=1228:goto1170

Reset sprite positions. Set background and border to purple. Reset do,co and s
770 x=138:y=133:x1=50:y1=80:c=1479:c1=1228:pokev+32,4:pokev+33,4:do=0:co=0:s=0
780 return

Sound effect
790 pokeh,30:fora=0to5:nexta:pokeh,0:return

Clear screen, clear frequency. en=reset eaten count  Increase fruit  fp=fruit placed=no
810 print"{clear}":pokeh+7,0:en=0:fr=fr+1:fp=0

Game screen set up
820 print"{home}{black}******************************"
830 print"{black}*{blue}q{red}..........................{blue}q{black}*"
840 print"{black}*{red}.{black}***{red}.{black}******{red}.{black}**{red}.{black}**{red}.{black}*****{red}.{black}***{red}.{black}*"
850 print"{black}*{red}.{black}***{red}.{black}******{red}.{black}**{red}.{black}**{red}.{black}*****{red}.{black}***{red}.{black}*"
860 print"{black}*{red}.{black}***{red}.{black}******{red}.{black}**{red}.{black}**{red}.{black}*****{red}.{black}***{red}.{black}*"
870 print"{black}*{red}.{black}***{red}....................{black}***{red}.{black}*"
880 print"{black}*{red}.{black}*******{red}.{black}**{red}.{black}*****{red}.{black}**{red}.{black}******{red}.{black}*"
890 print"{black}*{red}.{black}*******{red}.{black}**{red}.{black}*****{red}.{black}**{red}.{black}******{red}.{black}*"
900 print"{black}*{red}.{black}*******{red}.{black}**{red}.{black}*****{red}.{black}**{red}.{black}******{red}.{black}*"
910 print"{black}*{red}.{black}****{red}....{black}**{red}.{black}*****{red}.{black}**{red}....{black}***{red}.{black}*"
920 print"{black}*{red}......{black}**{red}.............{black}**{red}.....{black}*"
930 print"{black}*{red}......{black}*******{red}...{black}*******{red}.....{black}*"
940 print"{black}*{red}......{black}*******{red}...{black}*******{red}.....{black}*"
950 print"{black}*{red}......{black}**{red}.............{black}**{red}.....{black}*"
960 print"{black}*{red}.{black}****{red}....{black}***********{red}....{black}***{red}.{black}*"
970 print"{black}*{red}.{black}*******{red}.{black}***********{red}.{black}******{red}.{black}*"
980 print"{black}*{red}.{black}*******{red}.{black}**{red}.{black}*****{red}.{black}**{red}.{black}******{red}.{black}*"
990 print"{black}*{red}.{black}*******{red}.{black}**{red}.{black}*****{red}.{black}**{red}.{black}******{red}.{black}*"
1000 print"{black}*{red}.{black}***{red}....................{black}***{red}.{black}*"
1010 print"{black}*{red}.{black}***{red}.{black}*********{red}.{black}********{red}.{black}***{red}.{black}*"
1020 print"{black}*{red}.{black}***{red}.{black}******{red}.{black}**{red}.{black}**{red}.{black}*****{red}.{black}***{red}.{black}*"
1030 print"{black}*{red}.{black}***{red}.{black}******{red}.{black}**{red}.{black}**{red}.{black}*****{red}.{black}***{red}.{black}*"
1040 print"{black}*{blue}q{red}..........................{blue}q{black}*"
1050 print"{black}******************************{white}"
1060 print"{home}{down*2}";tab(31);"{red}score"
1070 print"{home}{down*6}";tab(31);"{red}high"
1080 print"{home}{down*10}";tab(31);"{red}lives"
1090 print"{home}{down*15}";tab(31);"maze{space*3}"
1100 print"{home}{down*17}";tab(31);"eater{space*2}"

Loop fruit
1101 iffr=197thenfr=192

Xy pos of spr0&1. c/c1=corresponding screen locations. Gosub colour change. Go to main routine
1110 x=138:y=133:x1=50:y1=80:c=1479:c1=1228:gosub650:goto270

Collision detection routine
If pac is in fruit zone and the fruit sprite is enabled then increase score. Switch off fruit sprite. Jump to sound routine. Fruit picked=yes
1130 ifx=138andy=133andpeek(v+21)=7thensc=sc+500:pokev+21,3:fp=1:goto1200

If collided with ghost but have eaten timer>0 then score increase by 100. Reset ghost sprite. Jump to sound routine
1140 ifpeek(v+30)=3anden>0thensc=sc+100:x1=50:y1=80:c1=1228:goto1229

If collided with ghost and eaten timer=0, then go to reduce lives
1150 if(peek(v+30)and2)=2anden=0thengoto710
1160 return

Sound effect: lost life
1170 pokewa,17:fora=0to200step30:pokeh,a:forb=0to50:nextb,a
1180 fora=200to0step-30:pokeh,a:forb=0to50:nextb,a:pokeh,0:pokev+30,0
1190 pokewa,33:return

Sound effect
1200 fora=0to100step30:pokeh,a:forb=0to10:nextb,a
1210 fora=100to0step-30:pokeh,a:forb=0to10:nextb,a:pokeh,0:pokev+30,0
1220 pokev+30,0:return

Sound effect
1229 fora=0to15:forb=0to5:pokeh+7,00+a*b:nextb,a:pokeh,0:return

$3200 to $32BE contains 3 pacman sprites
1230 ford=200to202:fora=0to62:readb:poked*64+a,b:nexta,d
1235 rem pac down
1240 data0,0,0,0,0,0,0,0,0,0,248,0,3,254,0,7,254,0,14,124,0,30,120,0,31,240,0
1250 data31,224,0,31,248,0,31,254,0,15,255,0,7,255,0,3,254,0,0,248
1260 data0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
1270 rem pac stopped
1280 data0,0,0,0,0,0,0,0,0,0,248,0,3,254,0,7,255,0,15,255,128,31,255,192,31
1290 data255,192,31,255,192,31,255,192,31,255,192,15,255,128,7,255,0,3,254,0,0
1300 data248,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
1310 rem pac left
1330 data 0,0,0,0,0,0,0,0,0,0,248,0,3,254,0,3,255,0,0,243,128,0,115,192,0,63
1340 data192,0,31,192,0,127,192,1,255,192,3,255,128,3,255,0,3,254,0,0,240
1350 data0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0

Fruit sprites (non zero not shown). In location $3000
1360 ford=192to196:fora=0to7:readb:poked*64+25+a*3,b:nexta,d
1370 data48,8,8,20,36,102,255,102
1390 data12,24,126,215,237,126,44,24
1410 data60,66,255,129,129,255,66,60
1430 data102,255,219,255,102,60,36,102
1450 data255,90,102,36,60,24,24,24

Char 42 = block
1460 fora=0to7:poke14336+42*8+a,255:next

Char 46 = dot
1470 fora=0to7:readb:poke14336+46*8+a,b:next
1480 data0,0,24,60,60,24,0,0

Char 32 = space
1490 fora=0to7:poke14336+32*8+a,0:next

Char 81 = pill
1500 fora=0to7:readb:poke14336+81*8+a,b:next
1510 data60,60,126,255,255,126,60,60

Char 65 = fruit1
1520 fora=0to7:readb:poke14336+65*8+a,b:next
1530 data48,8,8,20,36,102,255,102

Char 83 = fruit2
1540 fora=0to7:readb:poke14336+83*8+a,b:next
1550 data12,24,126,215,237,126,44,24

Char 88 = fruit3
1560 fora=0to7:readb:poke14336+88*8+a,b:next
1570 data60,66,255,129,129,255,66,60


Char 90 = fruit4
1580 fora=0to7:readb:poke14336+90*8+a,b:next
1590 data102,255,219,255,102,60,36,102

Char 87 = fruit5
1600 fora=0to7:readb:poke14336+87*8+a,b:next
1610 data255,90,102,36,60,24,24,24

$3340 to $343E = 4 ghost sprites
1620 ford=205to208:fora=0to62:readb:poked*64+a,b:nexta,d
1625 rem ghost up
1630 data2,128,0,10,160,0,42,168,0,170,170,0,158,182,0,158,182,0,150,150,0,150
1640 data150,0,170,170,0,170,170,0,170,170,0,170,170,0,170,170,0,170,170,0,136
1650 data136,0,136,136,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
1660 rem ghost right
1670 data2,128,0,10,160,0,42,168,0,170,170,0,150,150,0,158,158,0,158,158,0,150
1680 data150,0,170,170,0,170,170,0,170,170,0,170,170,0,170,170,0,170,170,0
1690 data136,136,0,136,136,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
1700 rem ghost down
1710 data2,128,0,10,160,0,42,168,0,170,170,0,150,150,0,150,150,0,158,182,0,158
1720 data182,0,170,170,0,170,170,0,170,170,0,170,170,0,170,170,0,170,170,0,136
1730 data136,0,136,136,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
1740 rem ghost left
1750 data2,128,0,10,160,0,42,168,0,170,170,0,150,150,0,182,182,0,182,182,0,150
1760 data150, 0,170,170,0,170,170,0,170,170,0,170,170,0,170,170,0,170,170,0,136
1770 data136,0,136,136,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0


bb$ holds “enter level” as array
1775 fora=0to10:readb$:bb$(a)=b$:next
1776 datae,n,t,e,r, ,l,e,v,e,l
1780 return

Sprite MSB=0
10000 pokev+16,0

Pac sprite xy=15, 108. Enable pac. pacptr=200. paccol=white. gg=screen address for sprite
10001 x=15:y=108:pokev,x:pokev+1,y:pokev+21,1:poke2040,200:pokev+39,1:gg=1340

Move sprite right in 8 pixel steps. Blanks out screen location. Switch sprite between 200 and 201 (i.e. animate)
10010 fora=0to255step8:gg=gg+1:pokev,a:pokegg,32:poke2040,200:poke2040,201

Sound effects
10020 pokeh,25:forb=0to10:nextb:pokeh,0:nexta

MSB for pac. Move sprite to edge of screen. Clear letters behind sprite and animate
10030 pokev+16,1:fora=0to60step8:gg=gg+1:pokev,a:pokegg,32:poke2040,200

Further animate. Sound effects.
10040 poke2040,201:pokeh,25:forb=0to10:nextb:pokeh,0:nexta:pokeh,0

MSB back to 0. X position=0, y position=116
10050 pokev+16,0:pokev,0:y=y+8:pokev+1,y

Repeat for the 7 lines of the menu title
10060 nu=nu+1:ifnu=7thennu=0:goto10080
10070 goto10010

Print “enter level” vertically and move sprite down screen
10080 y=50:x=22:pokev,x:print"{home}":poke2040,200
10090 fora=0to10:y=y+8:printchr$(158)bb$(a):pokev+1,y:forb=0to100:nextb,a:gg=1464

Print “whoops” then delete it
10091 print"{right*5}{white}whoops !!{yellow}":forb=0to400:nextb
10092 print"{up}{right*5}{space*9}"

Delete “enter level” and move sprite up the screen
10100 fora=0to10:y=y-8:pokegg,32:gg=gg-40:pokev+1,y:forb=0to100:nextb,a

Move sprite back down the screen
10110 fora=0to10:y=y+8:gg=gg+40:pokev+1,y:next

Move sprite across the screen
10120 fora=0to11:x=x+8:gg=gg+1:pokev,x:next:poke2040,200

Print “enter level” horizontally
10125 print"{right*13}";:y=y+8:pokev+1,y
10130 fora=0to5:x=x+8:printbb$(a);:pokev,x:forb=0to100:nextb,a
10135 print" ";:fora=6to10:x=x+8:printbb$(a);:pokev,x:forb=0to100:nextb,a

Pac pos = 180,80, ghost pos = 130,80, enable spr pac & ghost
10140 pokev,180:pokev+1,80:pokev+2,130:pokev+3,80:pokev+21,3

Expand sprites 0&1 in x&y direction. Sprite1=mulitcolour, ghostcol = red
10150 pokev+23,3:pokev+29,3:pokev+28,2:pokev+40,2:print

Instructions
10160 print"{red}{down*5} guide pacman around the three{space*10}"
10170 print"{up}{purple} types of maze whilst eating dots and "
10180 print"{yellow} power pods."

pc=pacman animation frame
10190 pc=200

Print level number. Le is more a reference to the difficulty level as it increases ghost speed
10200 print"{home}{down*13}";tab(24);le

Increase level number with press of f1
10210 getd$
10220 ifd$="{f1}"thenle=le+1
10230 ifle>5thenle=1

Animate pacman while waiting for key press
10231 poke2040,pc:pc=pc+1
10232 ifpc>201thenpc=200

Animate ghost while waiting for key press
10235 poke2041,hh:hh=hh+1
10236 ifhh>208thenhh=205

Check for space. If space then expand sprite 2 in x&Y (but not 0&1) and return
10240 ifpeek(197)=60thenpokev+23,4:pokev+29,4:return
10250 goto10200


If dead check score for hi-score and update. Show hi scores and set ga=1 (i.e. game already initialised)
20000 pokev+21,0:pokevo,0:pokev+32,0:pokev+33,0
20001 s$=str$(sc):n=len(s$):s$=left$("000000",8-n)+right$(s$,n-1):poke53272,21
20030 k=0:fori=1to5:ifsc>val(left$(b$(i),7))thenk=i:i=5
20040 nexti
20050 ifk=0then20100
20060 print"{blue}{clear}{down*2}{space*5}you are in the top five{down*2}":poke198,0
20070 print"{red}{space*7}enter your name{down}{blue}":input"";i$:iflen(i$)>10then20070
20080 fori=4tokstep-1:b$(i+1)=b$(i):next
20090 b$(k)=s$+" "+i$
20100 print"{clear}{space*5}gobbler all time greats{down*3}"
20110 fori=1to5:print"{down}{space*8}"i;b$(i):nexti:ifsc>hithenhi=sc
20120 ford=0to5000:next:ga=1:goto10
