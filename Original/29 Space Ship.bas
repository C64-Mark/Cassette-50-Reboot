!--------------------------------------------------
!- 28 October 2019 21:14:07
!- Import of : SHIP.PRG
!- From Disk : c:\users\marka\games\c64\cbmprg\cassette50reboot\spaceship.d64
!- Commodore 64
!--------------------------------------------------
0 POKE649,1:HS=5000:PRINTCHR$(14):PRINTCHR$(8):POKE788,52:POKE792,193:GOSUB8000
1 PRINT"{clear}":POKE53281,15:PRINT"{black}{right*11}SPACE{sh space}SHIP"
2 PRINT"{down*9}"
3 PRINT"{sh asterisk*4}WELCOME{sh space}TO{sh space}PLANET{sh space}OF{sh space}THE{sh space}DROIDS{sh asterisk*5}"
4 POKE53280,0:PRINT:PRINT:PRINT:PRINTSPC(12)"press any key"
5 R=1:X=120:Y=60:XG=100:YG=250:BO=1000:SC=0:D=2:LI=3                 
6 POKE649,1
10 V=54296:W=54276:A=54277:S=54278:HP=54275:LP=54274:H=54273:L=54272
20 POKEV,5:POKEW,65:POKE54283,129:POKE54284,8:POKE54286,8
25 POKEHP,12:POKELP,134
30 POKEA,8:POKES,8:POKEH,54:POKEL,111
40 POKE54280,48:POKE54279,127
70 POKEW,0:POKE54283,0
100 GETA$:IFA$=""THEN20
110 PRINT"{clear}":POKE53280,6:POKE53281,6:PRINT"{white}"
120 PRINTTAB(10)"INSTRUCTIONS"
130 PRINTTAB(9)"{cm t*14}"
133    
140 PRINT"You must enter several rooms in the base"
150 PRINT"in search of your space-craft which has"
160 PRINT"been stolen by the robot inhabitants of"
170 PRINT"this planet.Unfortunately the robots"
180 PRINT"will electrify you if they manage to"
190 PRINT"capture you!The robots are planning to"
200 PRINT"attack earth.So you must escape to give"
210 PRINT"a warning to earth's defences."
220 PRINT"{yellow}IF{sh space}YOU{sh space}FAIL THEN{sh space}THE{sh space}EARTH{sh space}IS{sh space}DOOMED!{white}"
230 PRINT"..............{yellow}GOOD{sh space}LUCK{white}.............."
240 PRINT:PRINT:PRINT:PRINTSPC(10)"PRESS{sh space}ANY{sh space}KEY"
250 GETA$:IFA$=""THEN250
290 PRINT"{clear}":POKE53281,15:PRINT"{black}"
300 DATA0,0,0,0,0,0,0,28,0,0,62,0,0,28,0,0,62,0,0,255,128
310 DATA0,255,128,0,190,128,0,190,128,0,190,128,0,190,128,0,190,128
320 DATA0,62,0,0,54,0,0,54,0,0,54,0,0,54,0,0,54,0,0,54,0
330 DATA0,247,128
340 POKE2041,13
350 FORG=0TO62:READH:POKE832+G,H:NEXTG
360 GR=53248
370 POKE2040,13
375 RESTORE
380 FORG=0TO62:READH:POKE896+G,H
390 POKEGR+21,3:POKEGR+39,6:POKEGR+40,0:POKEGR+29,1:POKEGR+23,1
391 FORF=0TO(D*10):RN=INT(RND(1)*1000)
392 POKE1024+RN,223:POKE55296+RN,9
393 NEXTF
395 FORF=0TO22:PRINTTAB(27)"{cm q}{cm w}{space*10}":NEXTF
396 PRINT"{home}"
400 PRINTTAB(29)"{black}SCORE";SC:PRINT
405 POKE1024+RN,223:POKE55296+RN,9
406                      
410 PRINTTAB(29)"ROOM";R:PRINT
415 PRINTTAB(29)"HIGH";HS
416 PRINTTAB(29)"LIVES";LI
420 PRINT"{home}"SPC(10)"{reverse on}{red}{space*2}EXIT{space*2}{black}{reverse off}"
430 POKEGR,X:POKEGR+1,Y
440 POKEGR+2,XG:POKEGR+3,YG
450 X=X-(DANDX>XG)+(DANDX<XG)
460 Y=Y-(DANDY>YG)+(DANDY<YG)
470 IF(PEEK(GR+30)AND2)=2ANDC=1THEN20000
480 IF(PEEK(GR+31)AND2)=2ANDC=1THEN10000
485 IF(PEEK(GR+31)AND1)=1ANDC=1THENGOSUB50000
490 IN=PEEK(56320)AND15
500 XG=XG-((D+2)ANDIN=11)+((D+2)ANDIN=7)
510 YG=YG+((D+2)ANDIN=13)-((D+2)ANDIN=14)
520 IFXG>220THENXG=220
530 IFXG<20THENXG=20
540 IFYG<50THENYG=50
550 IFYG>250THENYG=250
600 C=1:BO=BO-1:GOTO430
8000 POKE53280,11:POKE53281,11:POKE53270,255
8010 FORF=0TO120:PRINTSPC(5)"{light gray}{space*3}SPACE{sh space*2}SHIP{sh space*4}":NEXT
8020 POKE53270,200:RETURN
10000 IFYG>55THEN40000
10009 R=R+1:LI=LI+1:D=D+.3:SC=SC+(INT(BO))
10010 BO=1000
10025 POKE54276,0
10030 X=120:Y=60:XG=100:YG=250:C=0
10040 FORF=15TO0STEP-.25:POKE54296,F:POKE54276,33:POKE54277,8:POKE54273,40
10050 POKE54272,200:POKE54276,0:NEXTF
10060 PRINT"{clear}":GOTO391
20000 POKE54296,0:POKE54276,0
20001 FORF=0TO60
20010 POKEGR+40,(RND(1)*15)
20020 POKE54296,15:POKE54276,129
20030 POKE54277,6:POKE54278,6
20040 POKE54273,38:POKE54272,126
20050 POKE54276,0
20060 NEXTF
20061 POKEGR+40,0
20071 LI=LI-1:C=0:X=120:Y=60:XG=100:YG=250:IFLI>0THENPRINT"{clear}":GOTO391
20073 POKEGR+21,(PEEK(GR+21))AND253
20075 POKE53280,0:POKE53281,12
20080 PRINTSPC(9)"{down*11}{red}{reverse on}GAME{sh space}OVER{reverse off}{black}"
20090 IFSC>HSTHENHS=SC
20100 GETA$:IFA$=" "THENPOKEGR+21,(PEEK(GR+21))AND254:RESTORE:C=0:GOTO1
20110 GOTO20100
40000 POKE54276,129:POKE54283,33
40010 BO=0:C=0
40020 LI=LI-1:IFLI<1THEN20073
40030 IFR>5THENR=R-1
40040 XG=100:YG=250:PRINT"{clear}"
40050 POKE54276,0:GOTO395
50000 X=X+(2ANDX>XG)-(2ANDX<XG)
50010 Y=Y+(2ANDY>YG)-(2ANDY<YG)
50040 RETURN
