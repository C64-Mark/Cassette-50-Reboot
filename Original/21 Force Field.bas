!--------------------------------------------------
!- 27 October 2019 08:05:31
!- Import of : FORF.PRG
!- From Disk : c:\users\marka\games\c64\cbmprg\cassette50reboot\forcefield.d64
!- Commodore 64
!--------------------------------------------------
10 DIMG(10,2),S(10,2):POKE649,1:POKE650,255
100 GOSUB1000:PRINT"{red}":GOSUB2500:IFA$="q"THEN900
200 GOSUB3000
210 GOSUB3100:IFS2=1THENGOSUB3300:GOTO100
310 IFS3=0THENGOSUB3200:GOTO100
500 GOTO210
900 GOSUB1500:PRINT"end of game":STOP
1000 W=39:H=21:S1=0:J=0:S$="{space*39}":RETURN
1500 POKE53281,1:PRINT"{clear}";:RETURN
1600 PRINTCHR$(19);:Z8=X-1:IFZ8=0THEN1612
1610 FORU=1TOZ8:PRINTCHR$(29);:NEXTU
1612 Z9=Y-1:IFZ9=0THEN1690
1620 FORU=1TOZ9:PRINTCHR$(17);:NEXTU
1690 RETURN
1700 A$=S$:S=1:F=Z:GOSUB1900:PRINTM$:RETURN
1800 RA=RND(1):RETURN
1900 M$=MID$(A$,S,F-S+1):RETURN
2000 GOSUB4000:POKE54277,16:POKE54278,248:POKE54296,15:POKE54276,17:FORZ=50TO80:POKE54273,Z:POKE54272,Z:NEXTZ:GOSUB4000:RETURN
2300 INPUTQ$:RETURN
2400 IFO=1THENPRINTCHR$(18);
2410 IFO=0THENPRINT"
2490 RETURN
2500 GOSUB1500:PRINT"{reverse on}force field":PRINT:PRINT"instructions":PRINT:PRINT"you are trapped in a{space*2}force field with":PRINT"10 spongs and 10 fast moving gulpers!":PRINT"gulpers can move, but spongs can't.":PRINT"gulpers can eat you, spongs can eat you
2538 PRINT"and gulpers.":PRINT"{down}try & escape & destroy the gulpers":PRINT"{down}hint:try hiding behind spongs!":PRINT"{down}you are Q{space*2}q=up z=down{space*2}i=left p=right.":PRINT"{down*2}any key to play,{space*6}q to exit":X=1:Y=22:GOSUB1600:GOSUB2700:RETURN
2600 GETA$:RETURN
2700 GETA$:IFA$=""THEN2700
2790 RETURN
3000 GOSUB1500:S2=0:O=1:GOSUB2400:FORA=1TO10:GOSUB1800:X=INT(RA*W)+1:GOSUB1800:Y=INT(RA*H)+1:GOSUB1600:PRINT"g":G(A,1)=X:G(A,2)=Y:GOSUB1800:X=INT(RA*W)+1:GOSUB1800:Y=INT(RA*H)+1:GOSUB1600:GOSUB2400:PRINT"{green}s":S(A,1)=X:S(A,2)=Y:NEXTA:GOSUB1800:X1=INT(RA*W)+1:GOSUB1800:Y1=INT(RA*H)+1:X=X1:Y=Y1:GOSUB1600:PRINT"{purple}Q
3030 FORA=1TO10:IFS(A,1)=X1ANDS(A,2)=Y1THENS2=1
3032 NEXTA:IFS2=1THEN3000
3090 RETURN
3100 S3=0:FORA=1TO10:X=G(A,1):Y=G(A,2):IFX=0THEN3150
3104 S3=1:X5=SGN(X1-X):Y5=SGN(Y1-Y):GOSUB1600:PRINT" ":X=X+X5:Y=Y+Y5:G(A,1)=X:G(A,2)=Y:FORB=1TO10:IFX=S(B,1)ANDY=S(B,2)THENG(A,1)=0:GOSUB2000:S1=1
3120 NEXTB:IFS1=1THENS1=0:GOTO3150
3130 IFX=X1ANDY=Y1THENS2=1
3140 O=1:GOSUB2400:GOSUB1600:PRINT"{black}g":O=0:GOSUB2400
3150 NEXTA
3152 ZX=ZX+1:S9=1:X=X1:Y=Y1:GOSUB1600:PRINT" 
3160 GOSUB2600:IFA$="q"ANDY1<>1THENY1=Y1-1
3162 IFA$="z"ANDY1<>HTHENY1=Y1+1
3164 IFA$="i"ANDX1<>1THENX1=X1-1
3166 IFA$="p"ANDX1<>WTHENX1=X1+1
3167 IFS9=1THENS9=0:GOTO3160
3168 X=X1:Y=Y1:GOSUB1600:PRINT"{purple}Q":FORA=1TO10:IFS(A,1)=XANDS(A,2)=YTHENS2=1
3171 IFZX<2THENGOTO3152
3172 NEXTA:IFZX<2THENGOTO3152
3175 ZX=0:RETURN
3200 FORA=1TO5:X=3:Y=3:GOSUB1600:PRINT"{blue}{space*14}you win":GOSUB2000:NEXTA:RETURN
3300 X=3:Y=3:GOSUB1600:PRINT"{purple}{space*10}sorry you lose!":FORA=1TO5:X=10:Y=10:GOSUB1600:GOSUB2000:NEXTA:RETURN
4000 POKE54296,0:POKE54277,0:POKE54278,0:POKE54273,0:POKE54272,0:POKE54276,0:RETURN
